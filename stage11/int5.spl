alias physicalSp S0;
physicalSp = ([PTBR + 2 * (SP / 512)] * 512) + (SP % 512);

//System Call No 
alias sysCallNo S1;
sysCallNo = [physicalSp - 1];

if(sysCallNo == 8) then 
    alias pcbIndex S2;
    pcbIndex = 1;
    while(pcbIndex < 32) do 
        if([READY_LIST + pcbIndex * 32 + 1] == 0) then 
            break;
        endif;
        pcbIndex = pcbIndex + 1;
    endwhile;
    
    //Working Correctly 

    //NOT FOUND free PCB
    if(pcbIndex == 32) then 
        [physicalSp - 2] = -1;
        ireturn;
    endif;
    alias newPCB S3;
    newPCB = READY_LIST + 32 * pcbIndex;
    [newPCB + 0] = pcbIndex; 
    alias i S2;
    alias validPages S4;
    validPages = 0;
    i = 0;
    while(i < PTLR) do 
        if([PTBR + i * 2 + 0] != -1) then 
            validPages = validPages + 1;
        endif;
        i = i + 1;
    endwhile;
 
   

    //Counting Free pages in Memeory
    alias freePages S5;
    freePages = 0;
    i = 29;
    while(i < 64) do 
        if([MEM_LIST + i] == 0) then 
            freePages = freePages + 1;
        endif;
        i = i + 1;
    endwhile;

    //Working Correctly



    //Not enough pages in Memeory
    if(validPages > freePages) then 
        [physicalSp - 2] = -1;
        ireturn;
    endif;



    alias newPTBR S6;
    newPTBR = PAGE_TABLE + [newPCB + 0] * 8;
    
    //Working Correctly

    alias indexMem S7;
    alias parentPage S8;
    alias k S9;
    i = 0;
    while(i < PTLR) do  
        if([PTBR + i * 2 + 0] != -1) then 
            indexMem = 29;
            while(indexMem < 64) do 
                if([MEM_LIST + indexMem] == 0) then 
                    break;
                endif;
                indexMem = indexMem + 1;
            endwhile;
            [newPTBR + i * 2 + 0] = indexMem;
            [newPTBR + i * 2 + 1] = [PTBR + i * 2 + 1];
            //Copying data
            k = 0;
            parentPage = [PTBR + i * 2 + 0];
            while(k < 512) do 
                [0 + indexMem * 512 + k] = [0 + parentPage * 512 + k];
                k = k + 1;
            endwhile;
            [MEM_LIST + indexMem] = 1;
        else 
            [newPTBR + i * 2 + 0] = -1;
            [newPTBR + i * 2 + 1] = "00";
        endif;
        i = i + 1;
    endwhile;
    
    //Working Correctly


    //Copying Registers 
    [newPCB + 5] = newPTBR;
    [newPCB + 6] = PTLR;
    [newPCB + 4] = [physicalSp];
    [newPCB + 3] = [physicalSp - 1];
    [newPCB + 2] = BP;
    [newPCB + 7] = R0;
    [newPCB + 8] = R1;
    [newPCB + 9] = R2;
    [newPCB + 10] = R3;
    [newPCB + 11] = R4;
    [newPCB + 12] = R5;
    [newPCB + 13] = R6;
    [newPCB + 14] = R7;
    [newPCB + 31] = (PTBR -1024)/8;
    
    [physicalSp - 2] = [newPCB + 0];


    alias childSp S7;
    childSp = [newPCB + 3];
    alias physicalChildSp S8;
    physicalChildSp = ([newPTBR + 2 * (childSp / 512)] * 512) + (childSp % 512);

    [physicalChildSp - 1] = -2;
    [newPCB + 1] = 1;

    //Copying File Information 
    i = 15;
    alias parrentPCB S9;
    parrentPCB = READY_LIST + 32 * ((PTBR - 1024) / 8);
    while(i < 31) do 
        [newPCB + i] = [parrentPCB + i];
        i = i + 1;
    endwhile;
    i = 15;
    alias fileIndex S9;
    while(i < 31) do 
        fileIndex = [newPCB + i + 0];
	if(fileIndex<0 || fileIndex>63) then
		i=i+2;		
		continue;
	endif;
        [FILE_TABLE + 2 * fileIndex + 1] = [FILE_TABLE + 2 * fileIndex + 1] + 1;
        i = i + 2;
    endwhile;

endif;
ireturn;

// os startup code
[READY_LIST + 0] = 0;
[READY_LIST + 1] = 2; // running state
PTBR = 1024; 
PTLR = 4; 
[PTBR + 0] = 25; 
[PTBR + 1] = "01";	
[PTBR + 2] = 26; 
[PTBR + 3] = "01";	
[PTBR + 4] = 27; 
[PTBR + 5] = "01";	
[PTBR + 6] = 28; 
[PTBR + 7] = "01"; 
SP = 3 * 512; 
[28 * 512] = 0;
load(7,1);
load(8,2);
load(9,3);
load(10,4);	
load(11,5);
load(12,6);
load(13,7);
load(14,8);
load(15,9);
load(16,10);
load(17,11);
load(18,12);
load(19,13);
load(20,14);
load(21,15);
load(22,16);
load(23,17);
load(24,18);
load (5, 19);
load (6, 20);
load(25,21); 
load(26,22); 
load(27,23);

alias counter S0;			
counter = 0;
while ( counter <  128) do			
	[ FILE_TABLE + counter ] = -1;	
	[ FILE_TABLE + counter + 1] =  0; //SWOFT	
	counter = counter + 2;			
endwhile;

counter = 15;
while ( counter <= 30) do
	[READY_LIST + counter] =-1;   //PPOFT
	counter=counter+1;
endwhile;
counter = 0;
while ( counter<=28 ) do 	// used page in memory free list
	[1280 + counter ] =1;
	counter=counter+1;
endwhile;
while ( counter<64) do 
	[1280 + counter ] =0 ;//UNUSED pages in memory free list
	counter=counter+1;
endwhile;
counter = 1;
while( counter<32) do
	[READY_LIST + counter *32 + 1] =0; //state of all pcb's is 0 means terminated
	counter= counter+1;
endwhile;
ireturn;















// fork int 5

alias physicalSp S0;
physicalSp = ([PTBR + 2 * (SP / 512)] * 512) + (SP % 512);

//System Call No 
alias sysCallNo S1;
sysCallNo = [physicalSp - 1];

if(sysCallNo == 8) then 
    alias pcbIndex S2;
    pcbIndex = 0;
    while(pcbIndex < 32) do 
        if([READY_LIST + pcbIndex * 32 + 1] == 0) then 
            break;
        endif;
        pcbIndex = pcbIndex + 1;
    endwhile;
    

    //NO free PCB Found
    if(pcbIndex >= 32) then 
        [physicalSp - 2] = -1;
        ireturn;
    endif;

    //Found 
    alias newPCB S3;
    newPCB = READY_LIST + 32 * pcbIndex;
    [newPCB + 0] = pcbIndex; //setting PID



    //Counting valid pages of parent process
    alias i S2;
    alias validPages S4;
    validPages = 0;
    i = 0;
    while(i < PTLR) do 
        if([PTBR + i * 2 + 0] != -1 && ([PTBR + 2 * i + 1] == "01" || [PTBR + 2 * i + 1] == "11")) then 
            validPages = validPages + 1;
        endif;
        i = i + 1;
    endwhile;
 

    //Counting Free pages in Memeory
    alias freePages S5;
    freePages = 0;
    i = 29;
    while(i < 64) do 
        if([MEM_LIST + i] == 0) then 
            freePages = freePages + 1;
        endif;
        i = i + 1;
    endwhile;


    //Not enough pages in Memeory
    if(validPages > freePages) then 
        [physicalSp - 2] = -1;
        ireturn;
    endif;


    //Enough Free Pages Found
    alias newPTBR S6;
    newPTBR = PAGE_TABLE + [newPCB + 0] * 8;
    
    //Working Correctly

    alias indexMem S7;
    alias parentPage S8;
    alias k S9;
    i = 0;
    while(i < PTLR) do  
        if([PTBR + i * 2 + 0] != -1 && ([PTBR + 2 * i + 1] == "01" || [PTBR + 2 * i + 1] == "11")) then 
            indexMem = 29;
            while(indexMem < 64) do 
                if([MEM_LIST + indexMem] == 0) then 
                    break;
                endif;
                indexMem = indexMem + 1;
            endwhile;
            [newPTBR + i * 2 + 0] = indexMem;
            [newPTBR + i * 2 + 1] = [PTBR + i * 2 + 1];
            //Copying data
            k = 0;
            parentPage = [PTBR + i * 2 + 0];
            while(k < 512) do 
                [indexMem * 512 + k] = [parentPage * 512 + k];
                k = k + 1;
            endwhile;
            [MEM_LIST + indexMem] = 1;
        else 
            [newPTBR + i * 2 + 0] = [PTBR + 2 * i + 0];
            [newPTBR + i * 2 + 1] = [PTBR + 2 * i + 1];
        endif;
        i = i + 1;
    endwhile;
    
    //Working Correctly


    //Copying Registers 
    [newPCB + 2] = BP; 
    [newPCB + 3] = SP - 1;//logical
    [newPCB + 4] = [physicalSp]; 
    [newPCB + 5] = newPTBR;
    [newPCB + 6] = PTLR;
    [newPCB + 7] = R0;
    [newPCB + 8] = R1;
    [newPCB + 9] = R2;
    [newPCB + 10] = R3;
    [newPCB + 11] = R4;
    [newPCB + 12] = R5;
    [newPCB + 13] = R6;
    [newPCB + 14] = R7;
    //Returning PID of child to parent
    [physicalSp - 2] = [newPCB + 0];


    alias childSp S7;
    childSp = [newPCB + 3];
    alias physicalChildSp S8;
    physicalChildSp = ([newPTBR + 2 * (childSp / 512)] * 512) + (childSp % 512);

    [physicalChildSp - 1] = -2;
    [newPCB + 1] = 1; //State of child process

    //Copying File Information 
    i = 15;
    alias parrentPCB S9;
    parrentPCB = READY_LIST + 32 * ((PTBR - 1024) / 8);
    while(i < 31) do 
        [newPCB + i] = [parrentPCB + i];
        i = i + 1;
    endwhile;

    //Marking System wide open fileTable
    i = 0;
    alias fileIndex S9;
    while(i < 8) do 
        fileIndex = [newPCB + 15 + i * 2 + 0];
        if(fileIndex != -1) then
            [FILE_TABLE + 2 * fileIndex + 1] = [FILE_TABLE + 2 * fileIndex + 1] + 1;
        endif;
        i = i + 1;
    endwhile;

endif;
print("in int 5");
breakpoint;
ireturn;


// end int 5 fork
